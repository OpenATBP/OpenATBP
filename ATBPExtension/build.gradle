plugins {
    id "de.undercouch.download" version "5.4.0"
}

apply plugin: 'java'
group 'xyz.openatbp'
version '0.1.0-SNAPSHOT'

ext.getSFS2XPath = {
    if (System.getenv('SFS2X_HOME') == null) {
        return new File(System.getProperty('user.home'), 'SmartFoxServer_2X/SFS2X')
    } else {
        return new File(System.getenv('SFS2X_HOME'))
    }
}

tasks.register('copySFS2XLibs', Copy) {
    if (getSFS2XPath() == null) {
        throw new FileNotFoundException('Unable to find SFS2X - please install it or set up SFS2X_HOME environment variable.')
    }

    def SFS2XLibPath = new File(getSFS2XPath(), 'lib')

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from new File(SFS2XLibPath, 'sfs2x.jar'), new File(SFS2XLibPath, 'sfs2x-core.jar')
    into layout.projectDirectory.dir("lib")
}

tasks.register('downloadDefinitions', Download) {
    src 'https://archive.org/download/atbp-20150519/atbp-20150519-definitions.zip'
    dest buildDir
    overwrite false
}

tasks.register('extractDefinitions', Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from zipTree(layout.buildDirectory.file("atbp-20150519-definitions.zip"))
    into new File(getSFS2XPath(), 'extensions/Champions/')
}

tasks.register('copyDataFiles', Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from layout.projectDirectory.dir('deploy')
    into getSFS2XPath()
}

tasks.register('deploy', Copy ) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from layout.buildDirectory.file('libs/ATBPExtension.jar')
    into new File(getSFS2XPath(), 'extensions/Champions/')
}

tasks.withType(JavaCompile) {
    options.release = 11
}

jar {
    archiveFileName = "ATBPExtension.jar"
    manifest {
        attributes('Main-Class': 'xyz.openatbp.extension.ATBPExtension')
    }
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
}

dependencies {
    implementation files('lib/sfs2x-core.jar', 'lib/sfs2x.jar')
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.9.0'
    implementation 'org.mongodb:mongodb-driver-sync:4.10.0'
    compile 'com.github.implicit-invocation:jwalkable:master-SNAPSHOT'
}

assemble.dependsOn copySFS2XLibs

extractDefinitions.dependsOn downloadDefinitions
copyDataFiles.dependsOn extractDefinitions
deploy.dependsOn copyDataFiles
